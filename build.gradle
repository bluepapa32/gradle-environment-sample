defaultTasks 'clean', 'build'

apply plugin: 'java'

if (!hasProperty('env')) {
    env = System.env['GRADLE_ENV'] ?: 'development' 
}

{ ->
    def slurper = new ConfigSlurper()
    slurper.binding = project.properties

    def files = files("environment/default.gradle",
                  "environment/${env}.gradle",
                  "${System.properties['user.home']}/.gradle/environment.gradle")

    def config = files.inject(new ConfigObject()) { config, file ->
        file.exists() ? config.merge(slurper.parse(file.toURL())) : config
    }
    
    config.each { k, v -> setProperty k, v }
}()

processResources {
    expand (project.properties)
}

build << {
    println file("${sourceSets.main.classesDir}/Application.properties").text
}

